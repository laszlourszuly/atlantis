apply plugin: 'maven-publish'

publishing {
    publications {
        atlantis(MavenPublication) {
            groupId = 'com.echsylon.atlantis'
            artifactId = 'atlantis'
            version = android.defaultConfig.versionName

            // The publication doesn't know about our dependencies for the Android gradle plugin,
            // so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the compile dependencies (we don't want the test ones), adding a
                // <dependency> node for each
                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }

            def fileName = "${archivesBaseName}-${version}"

            // Source code bundle from task defined in 'archive.gradle'
            artifact("${buildDir}/outputs/jar/release/${fileName}-source.jar") {
                classifier 'source'
                extension 'jar'
            }

            // JavaDoc bundle from task defined in 'archive.gradle'
            artifact("${buildDir}/outputs/jar/release/${fileName}-javadoc.jar") {
                classifier 'javadoc'
                extension 'jar'
            }

            // The actual library from task defined in 'archive.gradle'
            artifact("${buildDir}/outputs/jar/release/${fileName}-fat.jar") {
                classifier 'fat'
                extension 'jar'
            }

            // The actual library from task defined in 'archive.gradle'
            artifact("${buildDir}/outputs/jar/release/${fileName}.jar") {
                extension 'jar'
            }
        }
    }
}

