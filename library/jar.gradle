task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.javadocDeps
    options {
        links 'http://docs.oracle.com/javase/8/docs/api/'
        linksOffline 'http://d.android.com/reference', "${android.sdkDirectory}/docs/reference"
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    destinationDir = file("${buildDir}/outputs/jar")
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    destinationDir = file("${buildDir}/outputs/jar")
}

task slimJar(type: Jar) {
    from("${buildDir}/intermediates/classes/release/") {
        exclude '**/BuildConfig.class'
        exclude '**/R$*.class'
        exclude '**/R.class'
    }
    destinationDir = file("${buildDir}/outputs/jar")
}

task fatJar(type: Jar) {
    // Add external dependency classes
    from configurations.compile.findAll {
        it.getName() != 'android.jar' && !it.getName().startsWith('junit') && !it.getName().startsWith('hamcrest')
    }.collect {
        it.isDirectory() ? it : zipTree(it)
    }
    // Add Atlantis classes
    from("${buildDir}/intermediates/classes/release/") {
        exclude '**/BuildConfig.class'
        exclude '**/R$*.class'
        exclude '**/R.class'
    }
    destinationDir = file("${buildDir}/outputs/jar")
}
